apply plugin: "java"
apply plugin: 'maven'

group = 'com.github.WilliamAHartman'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.main.resources.srcDirs = [ "res/",
                                      "libs/linux32", "libs/linux64",
                                      "libs/macosx32", "libs/macosx64",
                                      "libs/windows32", "libs/windows64",
				      "libs/linuxArm"]
sourceSets.test.java.srcDirs = [ ]
sourceSets.test.resources.srcDirs = [ ]

repositories {
    jcenter()
}

dependencies {
    //implementation files('/Users/richard/IdeaProjects/sdl2gdx/build/libs/sdl2gdx.jar')
    implementation "uk.co.electronstudio.sdl2gdx:sdl2gdx:1.0.4-beta3"
    //implementation "com.badlogicgames.gdx:gdx-jnigen:1.9.10"
}



task runTester(dependsOn: classes, type: JavaExec) {
    main = 'com.studiohartman.jamepad.tester.ControllerTester'
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
}


task dist(dependsOn: classes, type: Jar) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.compile.collect {zipTree(it)}}

    manifest {
        attributes 'Main-Class': 'com.studiohartman.jamepad.tester.ControllerTester'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'zlib License'
                    url 'http://www.zlib.net/zlib_license.html'
                    distribution 'repo'
                }
            }
        }
        pom.whenConfigured {
            p -> p.dependencies = []
        }
    }
}

